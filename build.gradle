apply {
	plugin 'java'
}

task wrapper(type: Wrapper) {
	distributionUrl = 'http://services.gradle.org/distributions/gradle-2.14-all.zip'
}

version = '1.0'

uploadArchives {
	repositories {
		flatDir name: 'development', dirs: "$buildDir/repository"
	}
}

repositories {
	add uploadArchives.repositories.getByName('development')

	mavenCentral()
}

sourceSets {
	main {
		java.srcDirs = ['production/source']
		resources.srcDirs = ['production/resources']
	}
	test {
		java.srcDirs = ['test/source']
		resources.srcDirs = ['test/resources']
	}
	releaseTest {
		java.srcDirs = ['release-test/source']
		resources.srcDirs = ['release-test/resources']
	}
}

dependencies {
	def junit = [group: 'junit', name: 'junit', version: '4.12']

	testCompile junit

	releaseTestCompile([
			[group: 'se.ericthelin', name: 'fractions', version: version],
			junit
	])
}

tasks.compileReleaseTestJava.dependsOn tasks.uploadArchives

task releaseTest(type: Test) {
	mustRunAfter tasks.test

	testClassesDir = sourceSets.releaseTest.output.classesDir
	classpath = sourceSets.releaseTest.runtimeClasspath
}

tasks.check.dependsOn tasks.releaseTest
